; [Comments translated from German to English]

; *****************************************************************************
; *                                                                           *
; *            S3 specific routines for the SVGA.BGI driver                   *
; *                                                                           *
; * (C) 1990-94 by                                                            *
; * Ullrich von Bassewitz                                                     *
; * Zwehrenb�hlstra�e 33                                                      *
; * D-72070 T�bingen                                                          *
; *                                                                           *
; * E-Mail: uz@ibb.schwaben.de                                                *
; *                                                                           *
; *****************************************************************************



; -----------------------------------------------------------------------
; Defines for the extended registers of the S3 chipset. The identifiers
; correspond to those in the manual
;

ADVFUNC_CNTL            = 04AE8h
CUR_Y                   = 082E8h
CUR_X                   = 086E8h
DESTY_AXSTP             = 08AE8h
DESTX_DIASTP            = 08EE8h
ERR_TERM                = 092E8h
MAJ_AXIS_PCNT           = 096E8h
CMD                     = 09AE8h
GP_STAT                 = 09AE8h
SHORT_STROKE            = 09EE8h
BKGD_COLOR              = 0A2E8h
FRGD_COLOR              = 0A6E8h
WRT_MASK                = 0AAE8h
RD_MASK                 = 0AEE8h
COLOR_CMP               = 0B2E8h
BKGD_MIX                = 0B6E8h
FRGD_MIX                = 0BAE8h
MULTIFUNC_CNTL          = 0BEE8h
MIN_AXIS_PCNT           = 0BEE8h
SCISSORS_T              = 0BEE8h
SCISSORS_L              = 0BEE8h
SCISSORS_B              = 0BEE8h
SCISSORS_R              = 0BEE8h
PIX_CNTL                = 0BEE8h
PIX_TRANS               = 0E2E8h


; -----------------------------------------------------------------------
; Various macros.
;

; Macro that waits until a certain number of places are free in the command FIFO

MACRO   S3_Wait CmdCount
LOCAL   L

        mov     dx, GP_STAT
L:      in      ax, dx
        cmp     al, 00FFh SHR CmdCount
        ja      L

ENDM


; Macro that outputs a value to an S3 port

MACRO   S3_Out  Port

        mov     dx, Port
        out     dx, ax

ENDM

; -----------------------------------------------------------------------
; Line routine using the S3 Graphics Engine
;
; Called with X1/Y1 = ax/bx, X2/Y2 = cx/dx
;

PROC    S3_Vect Near

; X and Y start values for later on the stack

        push    bx                      ; Y1
        push    ax                      ; X1

; Calculate various values

        mov     di, 10100000b

        sub     cx, ax                  ; cx = Abs (X2 - X1)
        jg      @@A1
        and     di, NOT 20h             ; X drawing direction is negative
        neg     cx
@@A1:   sub     dx, bx                  ; dx = Abs (Y2 - Y1)
        jg      @@A2
        and     di, NOT 80h             ; Y drawing direction is negative
        neg     dx
@@A2:   cmp     cx, dx
        jge     @@A3
        xchg    cx, dx
        or      di, 0040h
@@A3:   mov     bx, dx                  ; bx = Min, cx = Max

; Wait for free FIFO places

        S3_Wait 7

; Set foreground color and foreground mix

        mov     ax, 27h                 ; Normal mode
        cmp     [WriteMode], 0
        jz      @@L2
        mov     ax, 25h                 ; XOR mode
@@L2:   S3_Out  FRGD_MIX

	IF P80386        
		movzx   ax, [DrawingColor]
	ELSE
	        mov     al, [DrawingColor]
        	xor     ah, ah
	ENDIF

        S3_Out  FRGD_COLOR

; Now distinguish between solid/dashed lines

        cmp     [LinePattern], 0FFFFh
        jnz     @@L5

; Mask data selecting mix register

        mov     ax, 0A000h
        S3_Out  MULTIFUNC_CNTL

; Wait until 7 FIFO places are free

        S3_Wait 7

; Write start value for X/Y

        pop     ax                      ; X1
        S3_Out  CUR_X
        pop     ax                      ; Y1
        add     ax, [PageOfs]
        S3_Out  CUR_Y

; MAJ_AXIS_PCNT = Max

        mov     ax, cx                  ; cx = Max
        S3_Out  MAJ_AXIS_PCNT

; DESTX_DIASTP = 2 * (Min - Max)

        mov     ax, bx                  ; bx = Min
        sub     ax, cx                  ; cx = Max
        sal     ax, 1
        S3_Out  DESTX_DIASTP

; DESTY_AXSTP = 2 * Min

        mov     ax, bx                  ; bx = Min
        shl     ax, 1
        S3_Out  DESTY_AXSTP

        sub     ax, cx                  ; 2 * Min - Max
        test    di, 0020h
        jz      @@L3
        dec     ax
@@L3:   S3_Out  ERR_TERM

        mov     ax, 02013h
        or      ax, di
        S3_Out  CMD

        ret

; Line with pattern
; Set background mix and background color so that the background remains

@@L5:   mov     ax, 3
        S3_Out  BKGD_MIX

	IF P80386        
		movzx    ax, [BkColor]
	ELSE
	        mov     al, [BkColor]
        	xor     ah, ah
	ENDIF
        S3_Out  BKGD_COLOR

; Mask data selecting mix register provided by cpu

        mov     ax, 0A080h
        S3_Out  MULTIFUNC_CNTL

; Wait until 7 FIFO places are free

        S3_Wait 7

; Write start value for X/Y

        pop     ax                      ; X1
        S3_Out  CUR_X
        pop     ax                      ; Y1
        add     ax, [PageOfs]
        S3_Out  CUR_Y

; MAJ_AXIS_PCNT = Max

        mov     ax, cx                  ; cx = Max
        S3_Out  MAJ_AXIS_PCNT

; DESTX_DIASTP = 2 * (Min - Max)

        mov     ax, bx                  ; bx = Min
        sub     ax, cx                  ; cx = Max
        sal     ax, 1
        S3_Out  DESTX_DIASTP

; DESTY_AXSTP = 2 * Min

        mov     ax, bx                  ; bx = Min
        shl     ax, 1
        S3_Out  DESTY_AXSTP

        sub     ax, cx                  ; 2 * Min - Max
        test    di, 0020h
        jz      @@L6
        dec     ax
@@L6:   S3_Out  ERR_TERM

        mov     ax, 02313h
        or      ax, di
        S3_Out  CMD

; Output pattern

        mov     ax, [LinePattern]
        S3_Out  COLOR_CMP
        ret

ENDP    S3_Vect


; ======================================================================
;
; Ready: Waits until the graphics processor is idle
;

PROC    S3_Ready        NEAR

        push    dx
        push    ax
        mov     dx, GP_STAT
@@L1:   in      ax, dx
        test    ax, 0200h
        jnz     @@L1
        pop     ax
        pop     dx
        ret

ENDP    S3_Ready

; ======================================================================
;
; CLEAR: Clears the graphics screen
;
; The S3 command for drawing a rectangle is used
;

PROC    S3_Clear NEAR

; Wait until enough FIFO places are free, then set foreground mix, the color
; and the control register

        S3_Wait 3

        mov     ax, 0027h
        S3_Out  FRGD_MIX
        xor     ax, ax
        S3_Out  FRGD_COLOR
        mov     ax, 0A000h
        S3_Out  MULTIFUNC_CNTL

; Perform drawing operation

        S3_Wait 5

        xor     ax, ax
        S3_Out  CUR_X
        mov     ax, [PageOfs]
        S3_Out  CUR_Y
        mov     ax, [MaxX]
        S3_Out  MAJ_AXIS_PCNT
        mov     ax, [MaxY]
        S3_Out  MIN_AXIS_PCNT
        mov     ax, 040F3h
        S3_Out  CMD

        ret

ENDP    S3_Clear

; ======================================================================
;
;         DW      FILLSTYLE       ; Set the filling pattern
;
;   Input:
;      al         Primary fill pattern number
;      es:bx      If the pattern number is 0FFh, this points to user define
;                 pattern mask.
;
;   Return:
;      Nothing
;
;
PROC    S3_FillStyle    NEAR

; First call the old FillStyle to set the pattern

        call    FillStyle

; Now, for fast filling of patterned areas outside the visible screen,
; fill an 8x8 rectangle with the current pattern. For this, simply use
; PatBar (not S3_PatBar) and set the offset for the write page to 0 before.

        push    [PageOfs]
        xor     ax, ax
        mov     [PageOfs], ax
        mov     bx, [MaxY]
        mov     cx, 7
        lea     dx, [bx+7]
        call    PatBar
        pop     [PageOfs]

; Done!

        ret

ENDP    S3_FillStyle


;   ================================================================
;
;         DW      PATBAR          ; fill rectangle (X1,Y1), (X2,Y2)
;
;   Input:
;      ax         X1--the rectangle's left coordinate
;      bx         Y1--the rectangle's top coordinate
;      cx         X2--the rectangle's right coordinate
;      dx         Y2--the rectangle's bottom coordinate
;
;   Return:
;      Nothing
;
; Fill (but don't outline) the indicated rectangle with the current fill
; pattern and fill color.
;

PROC    S3_PatBar       NEAR

; Rearrange X1/Y1 and X2/Y2 so that X1<=X2 and Y1<=Y2.
; X2 and Y2 are only needed to calculate width and height and are not
; stored.
; The swapping at the beginning is unfortunately necessary, even if not
; documented...

        cmp     ax, cx
        jb      @@L1
        xchg    ax, cx
@@L1:   cmp     bx, dx
        jb      @@L2
        xchg    bx, dx

; Convert X1/Y1, X2/Y2 into X/Y and height/width

@@L2:   sub     cx, ax                  ; cx = width
        sub     dx, bx                  ; dx = height
        push    dx
        push    ax

        cmp     [FillPatternNum], SolidFill
        jz      @@L3
        cmp     [FillPatternNum], EmptyFill
        jz      @@L3

; It is a patterned rectangle, use PatBLT

        S3_Wait 2

        mov     ax, 0A000h
        S3_Out  MULTIFUNC_CNTL
        mov     ax, 0067h
        S3_Out  FRGD_MIX

; Perform drawing operation

        S3_Wait 7

        xor     ax, ax
        S3_Out  CUR_X
        mov     ax, [MaxY]
        S3_Out  CUR_Y
        pop     ax
        S3_Out  DESTX_DIASTP
        mov     ax, bx
        add     ax, [PageOfs]
        S3_Out  DESTY_AXSTP
        mov     ax, cx
        S3_Out  MAJ_AXIS_PCNT
        pop     ax
        S3_Out  MIN_AXIS_PCNT
        mov     ax, 0E0B3h
        S3_Out  CMD

        ret


; Wait until enough FIFO places are free, then set foreground mix, the color
; and the control register

@@L3:   S3_Wait 3

        mov     ax, 0027h
        S3_Out  FRGD_MIX

        xor     ax, ax
        cmp     [FillPatternNum], EmptyFill
        jz      @@L4
        mov     al, [FillColor]
@@L4:   S3_Out  FRGD_COLOR

        mov     ax, 0A000h
        S3_Out  MULTIFUNC_CNTL

; Perform drawing operation

        S3_Wait 5

        pop     ax
        S3_Out  CUR_X
        mov     ax, bx
        add     ax, [PageOfs]
        S3_Out  CUR_Y
        mov     ax, cx
        S3_Out  MAJ_AXIS_PCNT
        pop     ax
        S3_Out  MIN_AXIS_PCNT
        mov     ax, 040F3h
        S3_Out  CMD

        ret

ENDP    S3_PatBar


; ================================================================
;
; Draw a horizontal line in the current fill pattern without clipping
;
; Subroutine for FullEllipsePlot and FillPoly. Called with ax = Y,
; bx = X1, cx = X2, es = VideoSegment.
;
; ax, bx, cx, dx, si, di may be used freely
; The values that must be preserved: es, ds, bp, ss, cs
;

PROC    S3_HorLineInit  NEAR

        push    ax
        push    dx

; Wait for free FIFO places

        S3_Wait 5

; Set foreground color and foreground mix

        mov     ax, 27h                 ; Normal mode
        S3_Out  FRGD_MIX
	IF P80386        
		movzx     ax, [FillColor]
	ELSE
	        mov     al, [FillColor]
        	xor     ah, ah
	ENDIF

        S3_Out  FRGD_COLOR

; Set background mix and background color

        mov     ax, 07h
        S3_Out  BKGD_MIX

	IF P80386        
		movzx     ax, [BkColor]
	ELSE
	        mov     al, [BkColor]
        	xor     ah, ah
	ENDIF

        S3_Out  BKGD_COLOR

; Mask data selecting mix register provided by cpu

        mov     ax, 0A080h
        S3_Out  MULTIFUNC_CNTL

; And done

        pop     dx
        pop     ax
        ret

ENDP    S3_HorLineInit



PROC    S3_HorLine      NEAR

; Y start value for later on the stack

        mov     di, ax                  ; Y

; Get pattern

        mov     si, ax                  ; Y
        and     si, 7
        mov     al, [FillPattern+si]
        xchg    cx, bx                  ; cx = X1, bx = X2
        rol     al, cl
        xchg    cx, bx                  ; cx = X2, bx = X1
        sub     cx, bx                  ; cx = width-1
        mov     ah, al                  ; pattern in ax
        mov     si, ax                  ; pattern in si

; Wait until 7 FIFO places are free

        S3_Wait 7

; Write start value for X/Y

        mov     ax, bx                  ; X
        S3_Out  CUR_X
        mov     ax, di                  ; Y
        add     ax, [PageOfs]
        S3_Out  CUR_Y

; MAJ_AXIS_PCNT = Max

        mov     ax, cx                  ; cx = Max
        S3_Out  MAJ_AXIS_PCNT

; DESTX_DIASTP = 2 * (Min - Max)

        neg     cx                      ; cx = -Max
        mov     ax, cx                  ; ax = -Max
        sal     ax, 1                   ; 2 * -Max
        S3_Out  DESTX_DIASTP

; DESTY_AXSTP = 2 * Min

        xor     ax, ax                  ; Min = 0
        S3_Out  DESTY_AXSTP

        mov     ax, cx                  ; ax = -Max
        S3_Out  ERR_TERM

        mov     ax, 02337h
        S3_Out  CMD

; Output pattern

        mov     ax, si
        S3_Out  COLOR_CMP
        ret

ENDP    S3_HorLine


